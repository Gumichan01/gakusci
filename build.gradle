buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.google.cloud.tools.jib" version "$jib_version"
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
    id "org.jetbrains.kotlinx.atomicfu" version "$atomicfu_version"
    id 'io.ktor.plugin' version "$ktor_version"
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'io.gumichan01'
version '0.0.1-SNAPSHOT'

application {
    mainClass = 'io.gumichan01.gakusci.ApplicationKt'
}

kotlin {
    jvmToolchain(21)
}

// This task will generate the fat JAR and put it in the ./build/libs/ directory
jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': application.mainClass
    }

    dependsOn configurations.runtimeClasspath

    from {
        configurations.runtimeClasspath.findAll { File file ->
            file.name.endsWith('jar')
        }.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

distTar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

distZip {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jib {
    from {
        image = 'eclipse-temurin:21-alpine'
    }
    to {
        image = 'gakusci'
        tags = [version, 'latest'] as List<String>
    }
    container {
        mainClass = application.mainClass
        jvmFlags = ["-server", "-Xms128m", "-Xmx256m", "-XX:MaxMetaspaceSize=64m"]
        ports = ["80"]
    }
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "io.ktor:ktor-server-jetty-jakarta:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-server-thymeleaf:$ktor_version"
    implementation "io.ktor:ktor-client-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-jackson:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "com.bucket4j:bucket4j-core:$bucket4j_version"
    implementation "com.github.ouattararomuald:syndication:$syndication_kotlin"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "com.github.Gumichan01:jaikan-safe:$jaikan_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "io.mockk:mockk-jvm:$mockk_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"

}
