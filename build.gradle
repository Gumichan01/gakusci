buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.google.cloud.tools.jib' version '1.8.0'
}

apply plugin: 'kotlin'
apply plugin: 'application'

group 'io.gumichan01'
version '0.0.1-SNAPSHOT'
mainClassName = 'io.gumichan01.gakusci.ApplicationKt'

// This task will generate the fat JAR and put it in the ./build/libs/ directory
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
     }
}

jib {
    from {
        image = 'openjdk:alpine'
    }
    to {
        image = 'gakusci'
        tags = [version,'latest']
    }
    container {
        mainClass = mainClassName
        jvmFlags  = [ "-server", "-Xms128m", "-Xmx256m", "-XX:MaxMetaspaceSize=64m" ]
        ports = ["80"]
    }
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "io.ktor:ktor-server-jetty:$ktor_version"
    compile "ch.qos.logback:logback-classic:$logback_version"
    compile "io.ktor:ktor-server-core:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"
    compile "io.ktor:ktor-thymeleaf:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"
    compile "io.ktor:ktor-client-jackson:$ktor_version"
    implementation "com.ouattararomuald:syndication:$syndication_kotlin"
    testCompile "io.mockk:mockk:$mockk_version"
    testCompile "org.assertj:assertj-core:$assertj_version"
    testCompile "io.ktor:ktor-server-tests:$ktor_version"
}
