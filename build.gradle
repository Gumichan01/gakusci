buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlinx:atomicfu-gradle-plugin:$atomicfu_version"
    }
}

plugins {
    id "com.google.cloud.tools.jib" version "$jib_version"
    id "org.jetbrains.kotlin.jvm" version "$kotlin_version"
}

apply plugin: 'kotlin'
apply plugin: 'kotlinx-atomicfu'
apply plugin: 'application'

group 'io.gumichan01'
version '0.0.1-SNAPSHOT'
mainClassName = 'io.gumichan01.gakusci.ApplicationKt'

// This task will generate the fat JAR and put it in the ./build/libs/ directory
jar {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': mainClassName
    }

    dependsOn configurations.runtimeClasspath

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }
                      .collect { it.isDirectory() ? it : zipTree(it) }
     }
}

jib {
    from {
        image = 'openjdk:alpine'
    }
    to {
        image = 'gakusci'
        tags = [version, 'latest'] as List<String>
    }
    container {
        mainClass = mainClassName
        jvmFlags  = [ "-server", "-Xms128m", "-Xmx256m", "-XX:MaxMetaspaceSize=64m" ]
        ports = ["80"]
    }
}

sourceSets {
    main.kotlin.srcDirs = main.java.srcDirs = ['src']
    test.kotlin.srcDirs = test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
}

repositories {
    mavenLocal()
    jcenter() // Deprecated (Read-only repository)
    mavenCentral()
    maven { url 'https://kotlin.bintray.com/ktor' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "io.ktor:ktor-server-jetty:$ktor_version"
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-content-negotiation:$ktor_version"
    implementation "io.ktor:ktor-serialization-jackson:$ktor_version"
    implementation "io.ktor:ktor-server-thymeleaf:$ktor_version"
    implementation "io.ktor:ktor-client-apache:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "com.github.vladimir-bukhtoyarov:bucket4j-core:$bucket4j_version"
    implementation "com.ouattararomuald:syndication:$syndication_kotlin"
    implementation "com.github.ben-manes.caffeine:caffeine:$caffeine_version"
    implementation "com.github.GSculerlor:JikanKt:$jikankt_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "org.assertj:assertj-core:$assertj_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
}
